program ncdpost

  include 'netcdf.inc'
  integer istatus,ncid,dataid,dimid(2),rundimen,mzeta,mpoloidal,numberpe,nend,ndel,&
       nstart,mz,m,n,inqdim(NF_MAX_VAR_DIMS),inqlen(NF_MAX_VAR_DIMS),i,jt,mpsi,ii
  character(len=5) cdum
  character(9) vname,nameout
  character(len=12) namein
  character(len=20) fnamein
  integer,dimension(:),allocatable :: mtheta,igrid,itran
  real,dimension(:,:),allocatable :: eachdata,alldata

  write(*,*)'start_time, end_time, time_interval=?'
! open file for grid dimensions and other parameters
  namein='RUNdimen.ncd'
  istatus=nf_open(namein,nf_nowrite,ncid)

! read data
  istatus=nf_inq_varid(ncid,'PE-number',dataid)
  istatus=nf_get_var_int(ncid,dataid,numberpe)

  istatus=nf_inq_varid(ncid,'flux-surface-number',dataid)
  istatus=nf_get_var_int(ncid,dataid,mpsi)
  mpsi=mpsi-1
  allocate(mtheta(0:mpsi),igrid(0:mpsi),itran(0:mpsi))

  istatus=nf_inq_varid(ncid,'poloidal-grids',dataid)
  istatus=nf_get_var_int(ncid,dataid,mtheta)
  mtheta=mtheta-1

  istatus=nf_inq_varid(ncid,'index-shift',dataid)
  istatus=nf_get_var_int(ncid,dataid,itran)

! close netcdf data file
  istatus=nf_close(ncid)

! starting point for a poloidal grid
  igrid(0)=1
  do i=1,mpsi
     igrid(i)=igrid(i-1)+mtheta(i-1)+1
  enddo

! open data file
  write(*,*)'start_time, end_time, time_interval=?'
  read(*,*)nstart,nend,ndel

  do n=nstart-3,nend,ndel

     write(*,*)n," started"
     
! coordinates data
     if(n==nstart-3)then
        write(cdum,'("Xcoor")')
        vname='Xcoordina'
     elseif(n==nstart-2)then
        write(cdum,'("Ycoor")')
        vname='Ycoordina'
     elseif(n==nstart-1)then
        write(cdum,'("Zcoor")')
        vname='Zcoordina'
! potential data
     elseif(n<10)then
        write(cdum,'("P000",i1)')n
        vname='Potential'
     elseif(n<100)then
        write(cdum,'("P00",i2)')n
        vname='Potential'
     elseif(n<1000)then
        write(cdum,'("P0",i3)')n
        vname='Potential'
     else
        write(cdum,'("P",i4)')n
        vname='Potential'
     endif

! read each PE file
     do m=0,numberpe-1
        if(n<nstart)then
           if(m < 10)then
              write(namein,'("NCD",a5,".00",i1)')cdum,m
           elseif(m < 100)then
              write(namein,'("NCD",a5,".0",i2)')cdum,m
           else
              write(namein,'("NCD",a5,".",i3)')cdum,m
           endif           
! open NCD data file
           istatus=nf_open(namein,nf_nowrite,ncid)

        else
           write(fnamein,'("PHI_",i0,"_",i0,".ncd")')n,m
           istatus=nf_open(fnamein,nf_nowrite,ncid)

        endif

        if(m==0 .and. n==nstart-3)then
! real data dimension
           istatus=nf_inq_varid(ncid,'Xcoordina',dataid)
!           istatus=nf_inq_varid(ncid,'Potential',dataid)
           istatus=nf_inq_vardimid(ncid,dataid,inqdim)
           istatus=nf_inq_dimlen(ncid,inqdim(1),inqlen(1))
           istatus=nf_inq_dimlen(ncid,inqdim(2),inqlen(2))
           mpoloidal=inqlen(1)
           mz=inqlen(2)
! allocate memory
           mzeta=mz*numberpe+1
           allocate (eachdata(mpoloidal,mz),alldata(mpoloidal,mzeta))
        endif

        istatus=nf_inq_varid(ncid,vname,dataid)
        istatus=nf_get_var_real(ncid,dataid,eachdata)
        istatus=nf_close(ncid)

! check error
        if (istatus .ne. nf_noerr) then
           print *, nf_strerror(istatus)
        endif

        alldata(:,mz*m+2:mz*(m+1)+1)=eachdata
     enddo

! toroidal BC
     do i=0,mpsi
        ii=igrid(i)
        jt=mtheta(i)
        alldata(ii+1:ii+jt,1)=cshift(alldata(ii+1:ii+jt,mzeta),-itran(i))
        alldata(ii,1)=alldata(ii+jt,1)
     enddo

! write data to a single file
     write(nameout,'(a5,".ncd")')cdum
     istatus=nf_create(nameout,nf_clobber,ncid)
     istatus=nf_def_dim(ncid,'poloidal_grids',mpoloidal,dimid(1))
     istatus=nf_def_dim(ncid,'toroidal_grids',mzeta,dimid(2))
     istatus=nf_def_var(ncid,vname,nf_real,2,dimid,dataid)
     istatus=nf_enddef(ncid)
     istatus=nf_put_var_real(ncid,dataid,alldata)

! check error
        if (istatus .ne. nf_noerr) then
           print *, nf_strerror(istatus)
        endif
     istatus=nf_close(ncid)
     
     write(*,*)n,nameout," done"

  enddo

end program ncdpost
